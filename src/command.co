`#!/usr/bin/env node`

optimist = require "optimist"
    .usage "Transform a validatr formatted file or stdin\nUsage: $0 [options] file\n       $0 [options]"

    .options \help, do
        alias: \h
        describe: "Show this help"
        boolean: true

    .options \python, do
        alias: \y
        describe: "Compile to Python"
        boolean: true

    .options \javascript, do
        alias: \j
        describe: "Compile to Javascript"
        boolean: true

argv = optimist.argv

fs = require "fs"
util = require "util"
grammar = require "./grammar"
{ printVisitor, pythonVisitor, javascriptVisitor } = require "./vst"

function parse (data)
    try
        res = grammar.parse data
        # console.log "Done parsing"
        if argv.python
            return (pythonVisitor.visit res) + '\n'
        else if argv.javascript
            return (javascriptVisitor.visit res) + '\n'
        else
            return (printVisitor.visit res) + '\n'
    catch e
        console.log e.stack
        console.log "#{e.message}"
        return ""

if argv.help
    console.log optimist.help()
    process.exit 0

if argv._.length >= 1
    for file of argv._
        (err, data) <- fs.readFile file, 'utf-8', ...
        if err
            console.warn err.message
        else
            res = parse data
            util.print res
else
    process.stdin.resume()
    process.stdin.setEncoding "utf-8"

    # Read our file from stdin
    data = ""
    process.stdin.on "data", (chunk) ->
        data += chunk

    # When read, process it.
    <- process.stdin.on "end"
    util.print parse data

