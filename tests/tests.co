#!/usr/bin/env coco
vows = require \vows
{ join } = require \path
{ walk } = require \rbuild
{ readFileSync, stat } = require \fs
{ argv }: optimist = require \optimist
    .usage "$0: run the test suite."
    .alias \verbose, \v
    .alias \help, \h
    .describe \verbose, "Show all the tests"
    .describe \help, "You're staring at it"
{ spawn } = require \child_process
{ doesNotThrow, equal, notEqual } = require \assert
{ parallel } = require \async
util = require \util
vm = require \vm

{ parse } = require '../lib/grammar'
{ printVisitor, pythonVisitor, javascriptVisitor } = require '../lib/vst'

function execPython (python_code, json, done)
    code = """
        #python_code
        from simplejson import loads
        o = loads('''[ #json ]''')

        for obj in o:
            print unicode(validator.validate(obj)) + " : -" + unicode(obj) + "-"
        """

    cp = spawn "python", ['-c', code]

    stdout = ''
    stderr = ''
    cp.stdout.on \data, (data) ->
        stdout := stdout + data
    cp.stderr.on \data, (data) ->
        stderr := stderr + data

    cp.on \exit, ->
        done null, { stdout, stderr }

function execJs (js_code, json, done)
    code = """
        var exports = {};
        #js_code

        var o = [ #json ];
        var res = false;
        var ctx = null;

        for (var i = 0; i < o.length; i++) {
            ctx = new Context();
            res = exports.validator.validate(o[i], ctx);
            results.push([ res, o[i], ctx ]);
        }
    """

    sandbox = results: [], console: console
    exc = null

    try
        vm.runInNewContext code, sandbox
    catch e
        exc = e
    done null, results: sandbox.results, exception: exc


parseTests = vows.describe "Parsing Tests"
parseBatch = {}

if argv.v
    reporter = require \vows/lib/vows/reporters/spec
else
    reporter = require \vows/lib/vows/reporters/dot-matrix

function intent (cbk)
    try
        return cbk!
    catch e
        return null

walk "#__dirname", /.*\.vld$/, ((abs, rel) ->

    file = readFileSync rel, \utf-8
    [ vld, valid ? "", invalid ? "" ] = file.split '---'

    valid = valid.trim!
    invalid = invalid.trim!
    vld = vld.trim!

    parsed_vld = null
    python_code = null
    js_code = null

    intent -> parsed_vld := parse vld
    intent -> python_code := pythonVisitor.visit parsed_vld
    intent -> js_code := javascriptVisitor.visit parsed_vld

    test = parseBatch[rel] =
        topic: ->
            (err, results) <~ parallel do
                pyvalids: (cbk) ->
                    if not valid
                        cbk null
                        return
                    execPython python_code, valid, cbk
                pyinvalids: (cbk) ->
                    if not invalid
                        cbk null
                        return
                    execPython python_code, invalid, cbk
                jsvalids: (cbk) ->
                    if not valid
                        cbk null, []
                        return
                    execJs js_code, valid, cbk
                jsinvalids: (cbk) ->
                    if not invalid
                        cbk null, []
                        return
                    execJs js_code, invalid, cbk
            @callback err, results
                    

        "can be parsed without error": ->
            notEqual parsed_vld, null


    if parsed_vld
        test."compiles to python without error" = ->
            notEqual python_code, null

        test."can be printed and re-printed" = ->
            res1 = ""

            doesNotThrow function testPrint
                res1 := printVisitor.visit parsed_vld

            doesNotThrow function testRePrint
                res2 = parse res1
                equal res1, printVisitor.visit res2

    if python_code
        if valid
            test."python valids (#valid)" = ({ pyvalids }: obj) ->
                { stdout, stderr } = pyvalids
                equal stderr, ''
                equal (stdout.replace /^True.*$/mg , '' .replace /\n/g, ''), ''

        if invalid
            test."python invalids (#invalid)" = ({ pyinvalids }) ->
                { stdout, stderr } = pyinvalids
                equal stderr, ''
                equal (stdout.match /^True.*/m), null

    if js_code
        if valid
            test."javascript valids (#valid)" = ({ jsvalids }) ->
                { results, exception } = jsvalids

                equal results instanceof Array, true, "Results is an array"
                equal exception, null, "Got an exception: #{exception}"

                for v of results
                    equal v.0, true, "The following test failed: #{util.inspect(v.1)} with errors #{util.inspect(v.2.errors)}"
                #equal stderr, ''
                #equal (stdout.replace /^True.*$/mg , '' .replace /\n/g, ''), ''

        if invalid
            test."javascript invalids (#invalid)" = ({ jsinvalids }) ->
                { results, exception } = jsinvalids

                equal results instanceof Array, true, "Results is an array"
                equal exception, null, "Got an exception: #{exception}"

                for v of results
                    equal v.0, false, "The following test failed: #{util.inspect(v.1)} with errors #{util.inspect(v.2.errors)}"

    ), ->
        parseTests.addBatch parseBatch
        parseTests.run reporter: reporter
